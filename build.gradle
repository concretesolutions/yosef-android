buildscript {
    ext.kotlin_version = "1.2.20"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.0.1"
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:0.8.0"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply from: rootProject.file("tools/jacoco.gradle")
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.0"
}

task jacocoRootReport(type: JacocoReport) {
    group "Reporting"
    description "Generate Jacoco coverage reports."

    reports {
        xml.enabled = false
        html.enabled = true
    }

    def excludeProjects = ["sample"]

    // collecting all source sets, compiled classes and coverage binaries from sub projects
    def sourceSets = subprojects.findAll { project -> !excludeProjects.contains(project.name) }
            .collect { project -> project.jacocoReport.sourceDirectories }.flatten()
    def classDirs = subprojects.findAll { project -> !excludeProjects.contains(project.name) }
            .collect { project -> project.jacocoReport.classDirectories }.flatten()
    def coverageBinaries = subprojects.findAll { project -> !excludeProjects.contains(project.name) }
            .collect { project -> project.jacocoReport.executionData }.flatten()

    sourceDirectories = files(sourceSets)
    classDirectories = files(classDirs)
    executionData = files(coverageBinaries)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    kotlinVersion = kotlin_version
    minSdkVersion = 18
    targetSdkVersion = 27
    compileSdkVersion = 27
    buildToolsVersion = "27.0.2"

    supportVersion = "27.1.0"
    constraintVersion = "1.0.2"
    picassoVersion = "2.5.2"
    glideVersion = "4.6.1"
    jUnitVersion = "4.12"
    espressoVersion = "3.0.1"
    mockitoVersion = "2.12.0"
    mockitoKotlinVersion = "1.5.0"
    gsonVersion = "2.8.2"
    roboeletricVersion = "3.6.1"
    canarinhoVersion = "1.2.0"
    lottieVersion = "2.5.0-rc1"
    ktlintVersion = "0.19.0"
    detektVersion = "1.0.0.RC6-3"
}


apply from: "tools/detekt.gradle"
