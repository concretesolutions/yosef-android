apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka'

// Maven Group ID for the artifact
group = 'br.com.concrete'
version = rootProject.version

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId 'br.com.concrete'
            artifactId artifact

            // Add your description here
            name libraryName
            description libraryDescription
            url 'https://github.com/concretesolutions/yosef-android'

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection 'https://github.com/concretesolutions/yosef-android.git'
                developerConnection 'https://github.com/concretesolutions/yosef-android.git'
                url 'https://github.com/concretesolutions/yosef-android'
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
final Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']
    pkg {
        repo = 'concrete-maven'
        name = artifact
        userOrg = 'concrete'
        desc = libraryDescription
        websiteUrl = 'https://github.com/concretesolutions/yosef-android'
        vcsUrl = 'https://github.com/concretesolutions/yosef-android.git'
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty('bintray.gpg.password')
            }
        }
    }
}