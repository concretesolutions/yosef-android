apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    final build = rootProject.ext.buildVersions

    compileSdkVersion build.compileSdk

    defaultConfig {
        minSdkVersion build.minSdk
        targetSdkVersion build.targetSdk

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }

    buildTypes {

        debug {
            debuggable true
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {

    implementation project(':core')

    final libs = rootProject.ext.libsVersions
    implementation "com.android.support:appcompat-v7:$libs.support"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$libs.kotlin"
    api "com.squareup.picasso:picasso:$libs.picasso"

    // Instrumented tests
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // quality
    ktlint "com.github.shyiko:ktlint:$rootProject.ext.lintersVersions.ktlint"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

// Set up publication
ext {
    artifact = 'yosef-picasso'
    libraryName = 'Android Yosef Picasso Plugin'
    libraryDescription = 'Android Yosef plugin for Picasso views'
}

def localProps = project.rootProject.file('local.properties')

if (localProps.exists()) {
    final Properties properties = new Properties()
    properties.load(localProps.newDataInputStream())
    final user = properties.getProperty('bintray.user')
    final apikey = properties.getProperty('bintray.apikey')

    if (user && apikey)
        apply from: "$rootDir/tools/publish.gradle"
}
